/* ***************************
BG2-ToB Refinements Mod
Authors: T.G. Maestro, Littiz, the_bigg
WeiDU by Littiz, the_bigg
Adapted for EE circa 2016 by SubtleDoctor
**************************** */


BACKUP ~refinements/backup~
AUTHOR ~http://forums.spellholdstudios.net/index.php?showforum=116~
NO_IF_EVAL_BUG

// MODDER setup_tra none

ASK_EVERY_COMPONENT
AUTO_TRA ~refinements/languages/english~
AUTO_TRA ~refinements/%s~

README ~Refinements/README-Refinements.htm~
VERSION ~beta 4.21~


ALWAYS
// Uninstall the old versions
	UNINSTALL ~setup-refinements.tp2~ 0
	UNINSTALL ~setup-refinements.tp2~ 1
	UNINSTALL ~setup-refinements.tp2~ 2
	UNINSTALL ~setup-refinements.tp2~ 3
	UNINSTALL ~setup-refinements.tp2~ 4
	UNINSTALL ~setup-refinements.tp2~ 5

	// Corrections to KIT.IDS. Translates in WeiDU the corrections originally
	// made by Dyara, improved by Wisp and Camdawg

  ACTION_IF ENGINE_IS ~soa tob bgt~ BEGIN
	COPY_EXISTING ~kit.ids~ ~override~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(TRUECLASS[ %TAB%%LNL%%MNL%%WNL%]+\)~              ~0x4000 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(BERSERKER[ %TAB%%LNL%%MNL%%WNL%]+\)~              ~0x4001 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(WIZARDSLAYER[ %TAB%%LNL%%MNL%%WNL%]+\)~           ~0x4002 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(KENSAI[ %TAB%%LNL%%MNL%%WNL%]+\)~                 ~0x4003 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(CAVALIER[ %TAB%%LNL%%MNL%%WNL%]+\)~               ~0x4004 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(INQUISITOR[ %TAB%%LNL%%MNL%%WNL%]+\)~             ~0x4005 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(UNDEADHUNTER[ %TAB%%LNL%%MNL%%WNL%]+\)~           ~0x4006 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(MAGESCHOOL_ABJURER[ %TAB%%LNL%%MNL%%WNL%]+\)~     ~0x0040 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(MAGESCHOOL_CONJURER[ %TAB%%LNL%%MNL%%WNL%]+\)~    ~0x0080 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(MAGESCHOOL_DIVINER[ %TAB%%LNL%%MNL%%WNL%]+\)~     ~0x0100 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(MAGESCHOOL_ENCHANTER[ %TAB%%LNL%%MNL%%WNL%]+\)~   ~0x0200 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(MAGESCHOOL_ILLUSIONIST[ %TAB%%LNL%%MNL%%WNL%]+\)~ ~0x0400 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(MAGESCHOOL_INVOKER[ %TAB%%LNL%%MNL%%WNL%]+\)~     ~0x0800 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(MAGESCHOOL_NECROMANCER[ %TAB%%LNL%%MNL%%WNL%]+\)~ ~0x1000 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(MAGESCHOOL_TRANSMUTER[ %TAB%%LNL%%MNL%%WNL%]+\)~  ~0x2000 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(MAGESCHOOL_GENERALIST[ %TAB%%LNL%%MNL%%WNL%]+\)~  ~0x4000 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(FERALAN[ %TAB%%LNL%%MNL%%WNL%]+\)~                ~0x4007 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(STALKER[ %TAB%%LNL%%MNL%%WNL%]+\)~                ~0x4008 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(BEASTMASTER[ %TAB%%LNL%%MNL%%WNL%]+\)~            ~0x4009 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(ASSASIN[ %TAB%%LNL%%MNL%%WNL%]+\)~                ~0x400A \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(BOUNTYHUNTER[ %TAB%%LNL%%MNL%%WNL%]+\)~           ~0x400B \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(SWASHBUCKLER[ %TAB%%LNL%%MNL%%WNL%]+\)~           ~0x400C \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(BLADE[ %TAB%%LNL%%MNL%%WNL%]+\)~                  ~0x400D \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(JESTER[ %TAB%%LNL%%MNL%%WNL%]+\)~                 ~0x400E \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(SKALD[ %TAB%%LNL%%MNL%%WNL%]+\)~                  ~0x400F \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(GODTALOS[ %TAB%%LNL%%MNL%%WNL%]+\)~               ~0x4013 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(GODHELM[ %TAB%%LNL%%MNL%%WNL%]+\)~                ~0x4014 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(GODLATHANDER[ %TAB%%LNL%%MNL%%WNL%]+\)~           ~0x4015 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(TOTEMIC[ %TAB%%LNL%%MNL%%WNL%]+\)~                ~0x4010 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(SHAPESHIFTER[ %TAB%%LNL%%MNL%%WNL%]+\)~           ~0x4011 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(BEASTFRIEND[ %TAB%%LNL%%MNL%%WNL%]+\)~            ~0x4012 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(BARBARIAN[ %TAB%%LNL%%MNL%%WNL%]+\)~              ~0x40000000 \1~
	  REPLACE_TEXTUALLY ~^.+[ %TAB%]\(WILDMAGE[ %TAB%%LNL%%MNL%%WNL%]+\)~               ~0x80000000 \1~
	BUT_ONLY
	APPEND ~kit.ids~ ~0x4000 TRUECLASS~ UNLESS ~^.+[ %TAB%]TRUECLASS[ %TAB%%LNL%%MNL%%WNL%]+~
	APPEND ~kit.ids~ ~0x40000000 BARBARIAN~ UNLESS ~^.+[ %TAB%]BARBARIAN[ %TAB%%LNL%%MNL%%WNL%]+~
	APPEND ~kit.ids~ ~0x80000000 WILDMAGE~ UNLESS ~^.+[ %TAB%]WILDMAGE[ %TAB%%LNL%%MNL%%WNL%]+~
  END

// load NL variables
  OUTER_INNER_PATCH ~1~
  BEGIN
	WRITE_BYTE 0 0x0a
	READ_ASCII 0 lnl (1)
	WRITE_BYTE 0 0x0d
	READ_ASCII 0 mnl (1)
  END


// Needed for the broken entries in the key.
// Thanx Camdawg and others for pointing this out.
  OUTER_SET tb#fix_file_size_min = 0x72
  OUTER_SPRINT tb#fix_file_size_target "SW1H01.ITM"
  OUTER_SPRINT tb#fix_file_size_category "items"
  OUTER_SPRINT tb#fix_file_size_regexp "^.*\.itm$"
  LAUNCH_ACTION_MACRO ~tb#fix_file_size~

  INCLUDE ~Refinements/lib/hla_actions.tpa~

  INCLUDE ~Refinements/lib/fl#add_kit_ee.tpa~

  // Convert strings to UTF-8 for BG2EE
  // Using default_language is mandatory since english is used as fallback in case of missing string in a language
  ACTION_DEFINE_ARRAY tb#noconvert BEGIN prompts END
  ACTION_DEFINE_ARRAY tb#reload BEGIN setup miscscripts END
  LAF HANDLE_CHARSETS
    INT_VAR
      infer_charset = 1
    STR_VAR
      tra_path = EVAL ~%MOD_FOLDER%~ // Since %LANGUAGE% will include "languages/"
      iconv_path = EVAL ~%MOD_FOLDER%/languages/iconv~ // Must be set since iconv is not in tra_path
      default_language = ~languages/english~
      noconvert_array = tb#noconvert
      reload_array = tb#reload
  END

END  //ALWAYS


/* Language */
LANGUAGE ~English~ ~languages/english~ ~refinements/languages/english/setup.tra~
                                       ~refinements/languages/english/miscscripts.tra~
                                       ~refinements/languages/english/prompts.tra~

LANGUAGE ~French, by Sir Crapouilleau and Anomaly~ ~languages/french~   ~refinements/languages/english/setup.tra~
                                       ~refinements/languages/english/miscscripts.tra~
                                       ~refinements/languages/english/prompts.tra~
                                       ~refinements/languages/french/setup.tra~
                                       ~refinements/languages/french/miscscripts.tra~
                                       ~refinements/languages/french/prompts.tra~

LANGUAGE ~Japanese, by Kuroukome Veland~ ~languages/japanese~   ~refinements/languages/english/setup.tra~
                                       ~refinements/languages/english/miscscripts.tra~
                                       ~refinements/languages/english/prompts.tra~
                                       ~refinements/languages/japanese/setup.tra~
                                       ~refinements/languages/japanese/miscscripts.tra~
                                       ~refinements/languages/japanese/prompts.tra~

LANGUAGE ~Polish, by Yarpen and picollo~ ~languages/polish~   ~refinements/languages/english/setup.tra~
                                       ~refinements/languages/english/miscscripts.tra~
                                       ~refinements/languages/english/prompts.tra~
                                       ~refinements/languages/polish/setup.tra~
                                       ~refinements/languages/polish/miscscripts.tra~
                                       ~refinements/languages/polish/prompts.tra~

LANGUAGE ~Russian, by Aerie.ru, Prowler, Deneiris, Austin & Arcanecoast.ru~ ~languages/russian~   ~refinements/languages/english/setup.tra~
                                       ~refinements/languages/english/miscscripts.tra~
                                       ~refinements/languages/english/prompts.tra~
                                       ~refinements/languages/russian/setup.tra~
                                       ~refinements/languages/russian/miscscripts.tra~
                                       ~refinements/languages/russian/prompts.tra~


//////////////////////////////////////////////////////////////////////////////////
//Deprecated placeholders

// 0 - HLA
BEGIN @101
DEPRECATED @13
// 1 - Imoen
BEGIN @600
DEPRECATED @13
// 2 - Shapeshifting
BEGIN @700
DEPRECATED @13
// 3 - Robes
BEGIN @800
DEPRECATED @13
// 4 - Sword Angel
BEGIN @1001
DEPRECATED @13
// 5 - Armor Revision, in-house version
BEGIN @2000
DEPRECATED @13



//new stuff by subtledoctor


//////////////////////////////////////////////////////////////////////////////////
//New HLAs

BEGIN @101
DESIGNATED 10
REQUIRE_PREDICATE GAME_IS ~soa tob bgt bg2ee eet~ ~HLAs~
SUBCOMPONENT @100
//AT_INTERACTIVE_EXIT ~VIEW refinements/Docs/Revised_HLAs.txt~

INCLUDE ~refinements/lib/hlab.tpa~
INCLUDE ~refinements/lib/hla_fighter.tpa~
INCLUDE ~refinements/lib/hla_barbarian.tpa~
INCLUDE ~refinements/lib/hla_ranger.tpa~
INCLUDE ~refinements/lib/hla_paladin.tpa~
INCLUDE ~refinements/lib/hla_cleric.tpa~
INCLUDE ~refinements/lib/hla_druid.tpa~
INCLUDE ~refinements/lib/hla_monk.tpa~
INCLUDE ~refinements/lib/hla_wizard.tpa~
INCLUDE ~refinements/lib/hla_bard.tpa~
INCLUDE ~refinements/lib/hla_thief.tpa~

//////////////////////////////////////////////////////////////////////////////////

BEGIN @102
DESIGNATED 11
SUBCOMPONENT @100
//AT_INTERACTIVE_EXIT ~VIEW refinements/Docs/Revised_HLAs.txt~

INCLUDE ~refinements/lib/hlab.tpa~

//////////////////////////////////////////////////////////////////////////////////
//Fighter HLAs

BEGIN @10101
DESIGNATED 101
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~REFINEMENTS.TP2~ ~11~) ~...~

INCLUDE ~refinements/lib/hla_fighter.tpa~

//////////////////////////////////////////////////////////////////////////////////
//Barbarian HLAs

BEGIN @10102
DESIGNATED 102
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~REFINEMENTS.TP2~ ~11~) ~...~

INCLUDE ~refinements/lib/hla_barbarian.tpa~

//////////////////////////////////////////////////////////////////////////////////
//Ranger HLAs

BEGIN @10103
DESIGNATED 103
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~REFINEMENTS.TP2~ ~11~) ~...~

INCLUDE ~refinements/lib/hla_ranger.tpa~

//////////////////////////////////////////////////////////////////////////////////
//Paladin HLAs

BEGIN @10104
DESIGNATED 104
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~REFINEMENTS.TP2~ ~11~) ~...~

INCLUDE ~refinements/lib/hla_paladin.tpa~

//////////////////////////////////////////////////////////////////////////////////
//Cleric HLAs

BEGIN @10105
DESIGNATED 105
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~REFINEMENTS.TP2~ ~11~) ~...~

INCLUDE ~refinements/lib/hla_cleric.tpa~

//////////////////////////////////////////////////////////////////////////////////
//Druid HLAs

BEGIN @10106
DESIGNATED 106
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~REFINEMENTS.TP2~ ~11~) ~...~

INCLUDE ~refinements/lib/hla_druid.tpa~

//////////////////////////////////////////////////////////////////////////////////
//Monk HLAs

BEGIN @10107
DESIGNATED 107
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~REFINEMENTS.TP2~ ~11~) ~...~

INCLUDE ~refinements/lib/hla_monk.tpa~

//////////////////////////////////////////////////////////////////////////////////
//Wizard HLAs

BEGIN @10108
DESIGNATED 108
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~REFINEMENTS.TP2~ ~11~) ~...~

INCLUDE ~refinements/lib/hla_wizard.tpa~

//////////////////////////////////////////////////////////////////////////////////
//Bard HLAs

BEGIN @10109
DESIGNATED 109
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~REFINEMENTS.TP2~ ~11~) ~...~

INCLUDE ~refinements/lib/hla_bard.tpa~

//////////////////////////////////////////////////////////////////////////////////
//Thief HLAs

BEGIN @10110
DESIGNATED 110
REQUIRE_PREDICATE (MOD_IS_INSTALLED ~REFINEMENTS.TP2~ ~11~) ~...~

INCLUDE ~refinements/lib/hla_thief.tpa~

//////////////////////////////////////////////////////////////////////////////////




// end of subtledoctor's stuff... back to the bigg show





////////////////////////////////////////////////////////////////////////////////////
//Swashimoen, full revision.

BEGIN @600
DESIGNATED 20
SUBCOMPONENT @601

INCLUDE ~refinements/lib/swashimoen.tpa~



////////////////////////////////////////////////////////////////////////////////////
//Swashimoen, kit change only.

BEGIN @602
SUBCOMPONENT @601


// if TOB is not there (and there isn't an imoen15.cre) create it by copying from swashimoen213.cre
// (cheesy, but works  ;) )
ACTION_IF NOT FILE_EXISTS_IN_GAME ~imoen15.cre~ THEN
BEGIN
	COPY_EXISTING ~imoen213.cre~ ~override/imoen15.cre~
END

COPY_EXISTING ~imoen10.cre~ ~override~
			  ~imoen15.cre~ ~override~
			  ~imoen211.cre~ ~override~
			  ~imoen213.cre~ ~override~
	WRITE_SHORT   0x244 0    //needed to change the the kit
	WRITE_BYTE    0x246 12   //swashbuckler
	WRITE_BYTE    0x247 0x40 //needed to change the kit



////////////////////////////////////////////////////////////////////////////////////
//Swashimoen, Detect Illusions rather than Set Traps.

BEGIN @603
SUBCOMPONENT @601

INCLUDE ~refinements/lib/swashimoen.tpa~
COPY_EXISTING ~imoen10.cre~ ~override~
			  ~imoen15.cre~ ~override~
			  ~imoen211.cre~ ~override~
			  ~imoen213.cre~ ~override~
  WRITE_BYTE 0x64 60 // detect illusions
  WRITE_BYTE 0x65 0  // set traps




////////////////////////////////////////////////////////
//Shapeshifting fix


BEGIN @701
DESIGNATED 30
SUBCOMPONENT @700
COPY_EXISTING ~sw1h01.itm~ ~override/tb#shap_std.txt~
AT_INTERACTIVE_EXIT ~VIEW refinements/Docs/Shapefix.txt~
COPY ~refinements/shapefix/copy~	  	 ~override~



BEGIN @702
SUBCOMPONENT @700
FORBID_COMPONENT ~bg2fixpack.tp2~ 0 ~Already included in BG2 Fixpack~
COPY_EXISTING ~sw1h01.itm~ ~override/tb#shap_alt.txt~
AT_INTERACTIVE_EXIT ~VIEW refinements/Docs/Shapefix.txt~
COPY ~refinements/shapefix/alt/copy~	  	 ~override~
ACTION_IF FILE_EXISTS_IN_GAME ~tg#ussc.spl~ THEN BEGIN
  COPY ~refinements/shapefix/alt/LI#DRFIR.SPL~	 ~override/LI#DRFIR.SPL~
  SAY NAME1 @299
  SAY NAME2 @299
  COPY ~refinements/shapefix/alt/LI#DREAR.SPL~	 ~override/LI#DREAR.SPL~
  SAY NAME1 @298
  SAY NAME2 @298
  COPY ~refinements/shapefix/alt/LI#DRAIR.SPL~	 ~override/LI#DRAIR.SPL~
  SAY NAME1 @297
  SAY NAME2 @297
  COPY ~refinements/shapefix/alt/LI#DRWAT.SPL~	 ~override/LI#DRWAT.SPL~
  SAY NAME1 @296
  SAY NAME2 @296
END




////////////////////////////////////////////////////////
//Universal Robes


BEGIN @800
DESIGNATED 40

COPY_EXISTING ~CLCK09.ITM~	 ~override/CLCK09.ITM~
              ~CLCK10.ITM~	 ~override/CLCK10.ITM~
              ~CLCK11.ITM~	 ~override/CLCK11.ITM~
              ~CLCK12.ITM~	 ~override/CLCK12.ITM~
              ~CLCK13.ITM~	 ~override/CLCK13.ITM~
              ~CLCK14.ITM~	 ~override/CLCK14.ITM~
	READ_STRREF DESC desc
  SPRINT desc1 @807
	SAY_EVALUATED DESC ~%desc1%~
	WRITE_LONG 0x1e 0x40725ec0





////////////////////////////////////////////////////////////////////////////////////
//New Kit Installation

BEGIN @1001
DESIGNATED 50
//REQUIRE_FILE ~Data/25Dialog.bif~ @10
AT_INTERACTIVE_EXIT ~VIEW refinements/Docs/Sword_Angel.txt~

INCLUDE ~refinements/lib/swang.tpa~



// these are placeholders
BEGIN @2010
DESIGNATED 60
DEPRECATED @2010

BEGIN @2010
DEPRECATED @2010

BEGIN @2010
DEPRECATED @2010

BEGIN @2010
DEPRECATED @2010

////////////////////////////////////////////////////////////////////////////////////
//Revised Armors & Shields : description updater, only local language.

BEGIN @2003
SUBCOMPONENT @2000
DESIGNATED 70
REQUIRE_PREDICATE !(~%LANGUAGE%~ STRING_EQUAL_CASE ~languages/english~) @2005
FORBID_COMPONENT ~item_rev.tp2~ 0 @2012
COPY_EXISTING ~sw1h01.itm~ ~override/tb#localdescriptionsforarmor.txt~
INCLUDE ~refinements/lib/armor.tpa~

////////////////////////////////////////////////////////////////////////////////////
//Revised Armors & Shields : description updater, local language or English.

BEGIN @2004
SUBCOMPONENT @2000
REQUIRE_PREDICATE !(~%LANGUAGE%~ STRING_EQUAL_CASE ~languages/english~) @2005
FORBID_COMPONENT ~item_rev.tp2~ 0 @2012
COPY_EXISTING ~sw1h01.itm~ ~override/tb#generdescriptionsforarmor.txt~
INCLUDE ~refinements/lib/armor.tpa~

////////////////////////////////////////////////////////////////////////////////////
//Revised Armors & Shields : no description updater.

BEGIN @2007
SUBCOMPONENT @2000
REQUIRE_PREDICATE !(~%LANGUAGE%~ STRING_EQUAL_CASE ~languages/english~) @2005
FORBID_COMPONENT ~item_rev.tp2~ 0 @2012
INCLUDE ~refinements/lib/armor.tpa~

////////////////////////////////////////////////////////////////////////////////////
//Revised Armors & Shields : description updater, language = English.

BEGIN @2008
SUBCOMPONENT @2009
REQUIRE_PREDICATE (~%LANGUAGE%~ STRING_EQUAL_CASE ~languages/english~) @2006
FORBID_COMPONENT ~item_rev.tp2~ 0 @2012
COPY_EXISTING ~sw1h01.itm~ ~override/tb#generdescriptionsforarmor.txt~
INCLUDE ~refinements/lib/armor.tpa~

////////////////////////////////////////////////////////////////////////////////////
//Revised Armors & Shields : no description updater, language = English.

BEGIN @2007
SUBCOMPONENT @2009
REQUIRE_PREDICATE (~%LANGUAGE%~ STRING_EQUAL_CASE ~languages/english~) @2006
FORBID_COMPONENT ~item_rev.tp2~ 0 @2012
INCLUDE ~refinements/lib/armor.tpa~

////////////////////////////////////////////////////////////////////////////////////
//Revised Armors & Shields : no description updater, language = English.

BEGIN @2011
SUBCOMPONENT @2009
REQUIRE_PREDICATE (~%LANGUAGE%~ STRING_EQUAL_CASE ~languages/english~) @2006
FORBID_COMPONENT ~item_rev.tp2~ 0 @2012
COPY_EXISTING ~sw1h01.itm~ ~override/tb#nomovementratepenalties.txt~
COPY_EXISTING ~sw1h01.itm~ ~override/tb#generdescriptionsforarmor.txt~
INCLUDE ~refinements/lib/armor.tpa~
